#version 460
#extension GL_NV_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable

#include "../payload.h"


#define G_UNIFORM_INC_NV
#define G_UNIFORM_SET 0
#include "../global_uniforms.h"
#undef G_UNIFORM_SET
#undef G_UNIFORM_INC_NV

#define G_CACHE_SET 1
#include "../global_caches.h"
#undef G_CACHE_SET

layout(location = 0) rayPayloadNV InitialPayload payload;

void main() {
    const ivec2 xy = ivec2(gl_LaunchIDNV.xy);
    const vec2 xy_normalized = xy / vec2(gl_LaunchSizeNV.xy) - 0.5;

    const vec3 origin = UNI_CAMERA.origin;
    const vec3 direction = normalize(
        UNI_CAMERA.forward
        - xy_normalized.x * UNI_CAMERA.left
        + xy_normalized.y * UNI_CAMERA.up
    );

    const uint cullMask = 0xff;
    const float tmin = 0.1;
    const float tmax = 200.0;

    traceNV(UNI_TLAS, gl_RayFlagsNoneNV, cullMask, 0, 0, 0, origin, tmin, direction, tmax, 0);

    if (payload.hit) {
        imageStore(CACHE_NORMALS, xy, vec4(payload.normal, 0.0));
        imageStore(CACHE_INIT_DISTANCES, xy, vec4(payload.distance));
        imageStore(CACHE_ORIGIN, xy, vec4(payload.hit_position, 0.0));
        imageStore(CACHE_ILLUM_COEFFS, xy, vec4(payload.mer, 0.0));


        // FIXME: this is for debugging only, better to simply copy other caches
        imageStore(CACHE_PT_NORMALS, xy, vec4(payload.normal, 1.0));
        imageStore(CACHE_PT_ORIGINS, xy, vec4(payload.hit_position, 0.0));
        imageStore(CACHE_PT_ILLUM, xy, vec4(0.0));
    } else {
        imageStore(CACHE_NORMALS, xy, vec4(0));
        imageStore(CACHE_INIT_DISTANCES, xy, vec4(0.0));
        imageStore(CACHE_ORIGIN, xy, vec4(0));
        imageStore(CACHE_ILLUM_COEFFS, xy, vec4(0));

        // FIXME: this is for debugging only, better to simply copy other caches
        imageStore(CACHE_PT_NORMALS, xy, vec4(0.0));
        imageStore(CACHE_PT_ILLUM, xy, vec4(0.0));
    }

    imageStore(CACHE_DIRECT_ILLUM, xy, vec4(payload.illumination, 0.0));
}
